plugins {
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
    id "org.ajoberstar.grgit" version "1.5.1-rc.3"
}
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.openapi.generator'
openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/specs/elevator/elevator.yaml".toString()
    outputDir = "$buildDir/generated/server".toString()
    apiPackage = "men.chikagostory.elevator.api"
    invokerPackage = "men.chikagostory.elevator.invoker"
    modelPackage = "men.chikagostory.elevator.model"
    configOptions = [
            dateLibrary: "java8"
    ]
}

tasks.openApiGenerate{
    doFirst {
        mkdir("$buildDir/generated/server/")
        new File("$buildDir/generated/server/.openapi-generator-ignore").text = "**/*ApiController.java\n**/OpenAPI2SpringBoot.java\n"
    }
}

File genClientSrc = file("$buildDir/generated/server/src/main/java")
sourceSets.main.java.srcDir genClientSrc
idea.module.generatedSourceDirs += genClientSrc

ext {
    springfoxVersion = '2.8.0'
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compile "io.springfox:springfox-swagger2:${springfoxVersion}"
    compile "io.springfox:springfox-swagger-ui:${springfoxVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "javax.validation:validation-api"
    compileOnly 'org.projectlombok:lombok:1.18.6'
    annotationProcessor 'org.projectlombok:lombok:1.18.6'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

compileJava {
    dependsOn tasks.openApiGenerate
}

